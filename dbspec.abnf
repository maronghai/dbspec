; table

table_definition = table_header *field_definition

; table header

table_header = table_start SP table_name [SP comment] EOL
table_start  = %x23  ; #
table_name   = identifier

; field_definition

field_definition        = field_name SP field_type [SP auto_increment] [SP default_value] [SP comment] EOL
field_name              = identifier
field_type              = field_type_number / field_type_string / field_type_datetime
field_type_number       = field_type_number_int / field_type_number_float / field_type_number_n
field_type_number_int   = 1*2DIGIT                   ; 1-22
field_type_number_float = 1*2DIGIT %2E DIGIT         ; 0.0 - 22.9
field_type_number_nm    = %x6E / %x4E / %x6D / %x4D  ; [nNmM]
field_type_string       = %x73 *DIGIT                ; s[0-9]*
field_type_datetime     = %x74 [  %x31 / %x32 ]      ; t[12]
auto_increment          = 2%x2B                      ; ++
default_value           = %x5E SP *VCHAR             ; ^

; common definition

identifier   = ALPHA_ *ICHAR        ; [a-zA-Z_][a-zA-Z0-9_]*
comment      = 2%x2F *comment_char  ; //[ \ta-zA-Z0-9_]*
comment_char = WHITESPACE / VCHAR   ; [ \ta-zA-Z0-9_]*

; base definition

;; whitespace

WHITESPACE = %x20 / %x09  ; [ \t]
SP         = *WHITESPACE  ; [ \t]*

;; new line

EOL = [%x0D] %x0A

;; ALPHA DIGIT _ and CHAR

ALPHA  = %x41-5A / %x61-7A  ; [a-zA-Z]
DIGIT  = %x30-39            ; [0-9]
_      = %x5F               ; _
ALPHA_ = ALPHA / _          ; [a-zA-Z_]
ICHAR  = ALPHA / DIGIT / _  ; [a-zA-Z0-9_]
VCHAR  = %x20-7E

